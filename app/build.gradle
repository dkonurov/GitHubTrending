buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'realm-android'
apply from: '../config/quality/quality.gradle'
apply plugin: 'android-soexcluder'
apply plugin: 'spoon'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.firebase.firebase-crash'

repositories {
    maven { url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo' }
    maven { url "https://jitpack.io" }
    maven { url "https://maven.google.com" }
    maven { url 'https://maven.fabric.io/public' }
}

def getProps(String propName) {
    def propsFile = rootProject.file('local.properties')
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        return props[propName] != null ? props[propName] : "\"define config field: " + propName + "\""
    } else {
        return "\"define config file local.properties\"";
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'
    defaultConfig {
        applicationId "hram.githubtrending"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 5
        versionName "1.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "URL_BASE", '"https://github.com"'
        buildConfigField "String", "TELEGRAM_BOT_TOKEN", getProps("telegram.botToken")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            ext.enableCrashlytics = false
        }
    }

    productFlavors {
        realm {
        }
        freezer {
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    dexOptions {
        preDexLibraries true
        maxProcessCount 8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:25.4.0'
    compile 'com.android.support:recyclerview-v7:25.4.0'
    compile 'com.android.support:design:25.4.0'
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'com.github.florent37:retrojsoup:1.0.3'
    compile 'com.github.florent37:rxjsoup:1.0.3'
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'me.tatarka.bindingcollectionadapter2:bindingcollectionadapter:2.1.0'
    compile 'me.tatarka.bindingcollectionadapter2:bindingcollectionadapter-recyclerview:2.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.arello-mobile:moxy:1.5.3'
    compile 'com.arello-mobile:moxy-app-compat:1.5.3'
    testCompile('junit:junit:4.12') {
        exclude group: 'org.hamcrest'
    }
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.8.1'
    annotationProcessor 'com.github.florent37:retrojsoup-compiler:1.0.3'
    annotationProcessor 'com.arello-mobile:moxy-compiler:1.5.3'
    compile 'com.facebook.stetho:stetho:1.5.0'
    compile 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    realmCompile 'com.uphyca:stetho_realm:2.0.0'
    compile 'com.airbnb.android:lottie:2.0.0'

    compile "com.google.dagger:dagger:2.9"
    annotationProcessor "com.google.dagger:dagger-compiler:2.9"
    provided 'javax.annotation:jsr250-api:1.0'


    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'support-annotations'
    })
    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.8.1'
    androidTestCompile 'com.squareup.spoon:spoon-client:1.6.4'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
    compile 'fr.xebia.android.freezer:freezer:2.1.0'
    provided 'fr.xebia.android.freezer:freezer-annotations:2.1.0'
    annotationProcessor 'fr.xebia.android.freezer:freezer-compiler:2.1.0'
    compile 'com.github.viethoa:fastscroller:1.2.0'
    testCompile "org.robolectric:robolectric:3.3.2"
    testCompile 'com.github.pengrad:java-telegram-bot-api:3.1.0'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'com.github.johnkil.android-robototextview:robototextview:3.0.0'
    compile 'com.google.firebase:firebase-perf:11.0.2'
    compile 'com.google.firebase:firebase-crash:11.0.2'
}

soexcluder {
    freezer {
        exclude 'lib/mips/librealm-jni.so'
        exclude 'lib/x86_64/librealm-jni.so'
        exclude 'lib/x86/librealm-jni.so'
        exclude 'lib/arm64-v8a/librealm-jni.so'
        exclude 'lib/armeabi-v7a/librealm-jni.so'
    }
}

spoon {
    // for debug output
    debug = true

    // To run a single test class
    //className = 'fully.qualified.TestCase'

    // To run a single method in TestCase
    //methodName = 'testMyApp'

    // To execute the tests device by device */
    sequential = true

    // To grant permissions to Android M >= devices */
    grantAllPermissions = true
}

android.applicationVariants.all { variant ->
    variant.mergeAssets.doLast {
        File fonts = file("$variant.mergeAssets.outputDir/fonts")
        if (fonts.exists()) {
            for (File file : fonts.listFiles()) {
                if (!file.getName().contentEquals("Roboto-Light.ttf") && !file.getName().contentEquals("Roboto-Thin.ttf")) {
                    file.delete()
                };
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'